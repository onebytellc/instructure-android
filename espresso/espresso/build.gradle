/*
 * Copyright (C) 2016 - present Instructure, Inc.
 *
 *     Licensed under the Apache License, Version 2.0 (the "License");
 *     you may not use this file except in compliance with the License.
 *     You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 *     Unless required by applicable law or agreed to in writing, software
 *     distributed under the License is distributed on an "AS IS" BASIS,
 *     WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *     See the License for the specific language governing permissions and
 *     limitations under the License.
 */

apply plugin: 'com.android.library'

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
    mavenCentral()
}

buildscript {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
        mavenCentral()
    }
    dependencies {
        // https://bintray.com/android/android-tools/com.android.tools.build.gradle/view
        classpath 'com.android.tools.build:gradle:2.2.2'
        // https://github.com/JakeWharton/sdk-manager-plugin
        classpath 'com.github.JakeWharton:sdk-manager-plugin:e05218601b1274ea0721e13b33a426f641156f69'
    }
}

def getProperty(key, defaultValue) {
    rootProject.ext.hasProperty(key) ? rootProject.ext[key] : defaultValue
}

def _compileSdkVersion = getProperty('compileSdkVersion', 23)
def _buildToolsVersion = getProperty('buildToolsVersion', '23.0.3')
def _minSdkVersion = getProperty('minSdkVersion', '17')
def _targetSdkVersion = getProperty('targetSdkVersion', 23)
def _versionCode = getProperty('versionCode', 1)
def _versionName = getProperty('versionName', '1.0d')

android {
    compileSdkVersion _compileSdkVersion
    buildToolsVersion _buildToolsVersion

    defaultConfig {
        minSdkVersion _minSdkVersion
        targetSdkVersion _targetSdkVersion
        versionCode _versionCode
        versionName _versionName
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
        }
        release {
            minifyEnabled false
            shrinkResources false
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        // Required for Espresso.
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }
}

dependencies {
    // last update: June 20 2016
    // android-sdk-macosx ▸ extras ▸ android ▸ m2repository ▸ com ▸ android ▸ support ▸ test
    def runnerVersion = '0.5'
    def rulesVersion = '0.5'
    def espressoVersion = '2.2.2'
    def uiautomatorVersion = '2.1.2'
    def junitVersion = '4.12'

    compile 'com.linkedin.testbutler:test-butler-library:1.1.0'
    compile 'com.android.support:multidex:1.0.1'

    // Update exclusions based on ./gradlew :app:androidDependencies and
    // Android/sdk/extras/android/m2repository/com/android/support/test/* pom.xml
    compile("com.android.support.test:runner:$runnerVersion") {
        exclude module: 'support-annotations'
    }
    compile("com.android.support.test:rules:$rulesVersion") {
        exclude module: 'support-annotations'
    }
    compile("com.android.support.test.espresso:espresso-core:$espressoVersion") {
        exclude module: 'support-annotations'
    }
    // Fix IncompatibleClassChangeError https://code.google.com/p/android/issues/detail?id=183454
    compile("com.android.support.test.espresso:espresso-contrib:$espressoVersion") {
        exclude module: 'design'
        exclude module: 'recyclerview-v7'
        exclude module: 'support-v4'
        exclude module: 'support-annotations'
    }
    compile("com.android.support.test.espresso:espresso-idling-resource:$espressoVersion")
    compile("com.android.support.test.espresso:espresso-intents:$espressoVersion") {
        exclude module: 'support-annotations'
    }
    compile("com.android.support.test.espresso:espresso-web:$espressoVersion") {
        exclude module: 'support-annotations'
    }
    compile("com.android.support.test.uiautomator:uiautomator-v18:$uiautomatorVersion")
    compile("junit:junit:$junitVersion")
}
